buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

repositories {
    jcenter()
}

apply plugin: 'java'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.typesafe', name: 'config', version: '1.3.3'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

group = 'com.github.dahaka934'
version = '0.1.4'
description = 'Gson wrapper for Typesafe Config (HOCON)'

ext {
    artifactId = 'jhocon'
    gitUrl = 'https://github.com/Dahaka934/JHocon'
    licence = 'Apache-2.0'
}

publish {
    userOrg = 'dahaka934'
    groupId = project.group
    artifactId = project.artifactId
    publishVersion = project.version
    desc = project.description
    licences = [project.licence]
    website = project.gitUrl

    if (!project.hasProperty('bintrayUser')) {
        bintrayUser = System.getenv('BINTRAY_USER')
    }
    if (!project.hasProperty('bintrayKey')) {
        bintrayKey = System.getenv('BINTRAY_KEY')
    }
    if (!project.hasProperty('dryRun')) {
        dryRun = false
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    tasks.generatePomFileForMavenPublication.doLast {
        file("build/publications/maven/pom-default.xml ").delete()
        pom {
            project {
                name "$project.name"
                artifactId project.artifactId
                packaging project.name == 'compiler' ? 'jar' : 'aar'
                description project.description
                url project.gitUrl
                version project.version

                scm {
                    url project.gitUrl
                    connection project.gitUrl
                    developerConnection project.gitUrl
                }

                licenses {
                    license {
                        name project.licence
                    }
                }

                developers {
                    developer {
                        id 'Dahaka934'
                        name 'Mikhail Abakumov'
                        email 'dahaka934@gmail.com'
                    }
                }
            }
        }.writeTo("build/publications/maven/pom-default.xml")
    }
}
